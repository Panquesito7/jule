// Copyright 2022 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

#pragma enofi

type TestTypeAlias: i32

// Test for global variable.
let TEST_GLOBAL = 10;
// Test for global constant variable.
const TEST_CONST = 20

// Test for inline function.
//jule:inline
fn test_inline_function() {}

fn test_string_concat() {
	_ = "Hello" + " " + "World!"
}

fn test_intergers() {
	_ = 13e+1
	_ = 3536
	_ = 0x0FDDA24
	_ = 05353
}

fn test_variable() {
	let a: int
	_ = a
	let x = 10;
	x = 100
	outln(x);
}

fn test_assignment() {
	let x = 10
	x += 100;
}

fn test_slice() {
	let my_slice = [][]str{
		[]str{"Apple", "Banana"},
		[]str{"Bred", "Cheese"},
	};
	outln(
		my_slice[0]
	)
}

fn test_array() {
	let my_array: [2][3]str = [2][3]str{
		[3]str{"Apple", "Banana", "TEST"},
		[3]str{"Bred", "Cheese", "BLA"},
	};
	outln(
		my_array[0]
	)
}

fn test_anonymous_function() {
	let my_func: fn() = fn() {
		outln("Hello World!")
	}
	my_func()
	let rf = fn() i32 {
		ret 90
	}()
	_ = rf
	fn() {
		outln("Anonymous Outln")
	}()
	let a = 0
	_ = a
}

fn test_multiple_variable() {
	let (a, b, _, c) = 100, 200, nil, false
	_ = c
	a, b = 10, 20
	a, b = b, a
	outln(a)
	outln(b)
	a, b, _ = test_multiple_return()
}

fn test_multiple_return() (i32, i32, bool) { ret 5, 5, false }

fn test_iter() {
	// Infinity iteration
	for {
		break
		continue
	}

	// While iteration
	for !false {
		break
	}

	// Foreach iteration
	for in "Hello" {
		outln("Jule")
	}

	for index: i32 in "Hello" {
		outln(index)
	}

	for _, c: in "Hello" {
		outln(c)
	}

	for _, element: str in ([]str{"The", "Jule", "Programming", "Language"}) {
		outln(element)
	}

	for index: in "TEST" {
		_ = index
	}

	for ; ; { break }

	for let a = 0; a <= 3; a++ {
		outln(a)
	}
}

fn test_if_expressions() {
	if true {
		outln("IF")
	} else if true { outln("ELSE_IF0") } else if false {
		outln("ELSE_IF_1")
		if true == true
			{ outln("ELSE_IF_1_IF"); }
		else {
			outln("ELSE");
		}
	} else {
		outln("ELSE")
	}
}

fn test_parameters(a: i32, b: bool) {}

fn test_parameters_2(a: i32, b: i32, c: bool, d: bool) {}

fn test_variadic_parameters(ln_every: bool, values: ...i32) {
	for _, i: in values {
		if ln_every { outln(i) }
		else        { out(i) }
	}
}

fn test_variadic_parameters_2(...str) {}

fn test_casting() {
	let a: *int
	_ = (uintptr)(a)
	_ = (f32)(5000)
	let bytes = ([]byte)("Hello Bytes")
	outln(bytes)
	outln((str)(bytes))
}

fn test_raw_string() {
	_ = `Hello
	raw`

	_ = `
  strings`
}

fn test_map() {
	let map = [int:str]{
		0: "The",
		1: "Jule",
		2: "Programming",
		3: "Language"
	}
	map[4] = "Maps"
	outln(map)
	for key:, value: in map {
		out(key)
		out(": ")
		outln(value)
	}
}

fn test_multiple_return_as_arguments() {
	let test = fn(a: i32, b: str, c: byte) {
		outln(a)
		outln(b)
		outln(c)
	}
	let multiret = fn() (i32, str, byte) { ret 134, "STR", 'B' }
	test(multiret())
}

fn test_multiple_return_as_multiple_return() {
	let test1 = fn() (i32, str, byte) { ret 143, "STR", 'W' }
	let test2 = fn() (i32, str, byte) { ret test1() }
	let (a, b, c) = test2()
	outln(a)
	outln(b)
	outln(c)
}

fn test_inblock_type_alias() {
	type integer: i32
	let x: integer = 10
	_ = x
}

fn test_inblock_block() {
	{
		outln("INBLOCK_BLOCK")
	}
}

fn test_deferred_calls() {
	defer fn() { outln("Defer 1") }()
	defer fn() { outln("Defer 2") }()
}

fn test_goto() {
	let s = "Hello World"
	{
		let ok = false
		{
			if !ok {
				ok = true
				goto bla
			}
			goto test
		}
	bla:
		outln(ok)
	}
test:
	{
		outln("TEST")
		goto end
	}
	outln(s)
end:
}

enum test_enum {
	item1,
	item2,
	item3 = 369,
	item4,
}

struct test_struct {
	a: str
	b: i32
	c      = 10
	d: bool = false
}

impl test_struct {
	fn func1() {
		self.a = "test"
	}

	type[T1]
	fn func2() {
		self.d = !self.d
	}
}

type[T]
fn test_generic_func(a: T, b: T) T { ret a+b }

fn test_ret_vars() (x: int) { ret }

fn test_suffix() {
	let a = 10
	a++
	a--
}

fn test_match_case() {
	match 10 {
	case 1:
		outln("one")
	case 2:
		outln("two")
	default:
		outln("default")
	}

	match {
	case false:
		outln("false")
	case !true:
		outln("!true")
	case false, true:
		outln("false, true")
	default:
		outln("default")
	}
}

fn init() {
	outln("Syntax Test")
}

// Entry point function of program.
fn main() {
	test_inline_function()
	test_intergers()
	test_string_concat();
	test_variable();
	test_assignment()
	test_slice()
	test_array()
	test_anonymous_function()
	test_multiple_variable();
	test_iter()
	test_if_expressions();
	test_parameters(24, false)
	test_parameters_2(24, 52, true, false)
	test_variadic_parameters(true);
	test_variadic_parameters(true, 10, 20);
	test_variadic_parameters(!false, []i32{1, 2, 3, 4, 5}...)
	test_variadic_parameters_2()
	test_variadic_parameters_2("TEST", "TEST_1")
	test_casting()
	test_raw_string()
	test_map()
	test_multiple_return_as_arguments()
	test_multiple_return_as_multiple_return()
	test_inblock_type_alias()
	test_inblock_block()
	test_deferred_calls()
	test_goto()
	outln(test_enum.item3)
	test_generic_func[int](2, -30)
	test_generic_func[uint](6, 2)
	test_generic_func[f64](4.2, 35.23)
	test_match_case()
}
