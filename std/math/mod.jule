// Copyright 2022 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

/*
  Floating-point mod function.
*/

// Returns the floating-point remainder of x/y.
// The magnitude of the result is less than y and its
// sign agrees with that of x.
//
// Special cases are:
//  mod(±inf, y) = nan
//  mod(nan, y) = nan
//  mod(x, 0) = nan
//  mod(x, ±inf) = x
//  mod(x, nan) = nan
pub fn mod(x: f64, y: f64) f64 {
	if y == 0 || is_inf(x, 0) || is_nan(x) || is_nan(y) {
		ret nan()
	}
	y = abs(y)

	let (yfr, yexp) = frexp(y)
	let r = x
	if x < 0 {
		r = -x
	}

	for r >= y {
		let (rfr, rexp) = frexp(r)
		if rfr < yfr {
			rexp = rexp - 1
		}
		r = r - ldexp(y, rexp-yexp)
	}
	if x < 0 {
		r = -r
	}
	ret r
}
