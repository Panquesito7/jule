// Copyright 2022 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

// Returns the binary exponent of x.
//
// Special cases are:
//  logb(±inf) = +inf
//  logb(0) = -inf
//  logb(nan) = nan
pub fn logb(x: f64) f64 {
	// special cases
	match {
	case x == 0:
		ret inf(-1)
	case is_inf(x, 0):
		ret inf(1)
	case is_nan(x):
		ret x
	}
	ret f64(_ilogb(x))
}

// Returns the binary exponent of x as an integer.
//
// Special cases are:
//  ilogb(±inf) = i32.max
//  ilogb(0) = i32.min
//  ilogb(nan) = i32.max
pub fn ilogb(x: f64) int {
	// special cases
	match {
	case x == 0:
		ret i32.min
	case is_nan(x):
		ret i32.max
	case is_inf(x, 0):
		ret i32.max
	}
	ret _ilogb(x)
}

// Returns the binary exponent of x. It assumes x is finite and non-zero.
fn _ilogb(mut x: f64) int {
	let mut exp: int = 0
	x, exp = normalize(x)
	ret int((f64_bits(x)>>SHIFT)&MASK) - BIAS + exp
}
