// Copyright 2022 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::errors

// Indicates that a value is out of range for the target type.
pub let ERROR_RANGE = std::errors::new("value out of range")

// Indicates that a value does not have the right syntax for the target type.
pub let ERROR_SYNTAX = std::errors::new("invalid syntax")

// Records a failed conversion.
pub struct ConvError {
	Func : str    // the failing function
	Input: str    // the input
	Error: Error  // the reason the conversion failed
}

impl Error for ConvError {
	fn error() str {
		ret "std::conv::" + .Func + ": " + "parsing " + .Input + ": " + .Error.error()
	}
}

impl ConvError {
	pub fn &unwrap() Error {
		ret .Error
	}
}

fn syntax_error(func: str, input: str) &ConvError {
	ret &ConvError{
		Func: func,
		Input: input,
		Error: ERROR_SYNTAX,
	}
}

fn range_error(func: str, s: str) &ConvError {
	ret &ConvError{
		Func: func,
		Input: s,
		Error: ERROR_RANGE,
	}
}

fn base_error(func: str, s: str, base: int) &ConvError {
	ret &ConvError{
		Func: func,
		Input: s,
		Error: std::errors::new("invalid base " + itoa(base)),
	}
}

fn bit_size_error(func: str, s: str, bit_size: int) &ConvError {
	ret &ConvError{
		Func: func,
		Input: s,
		Error: std::errors::new("invalid bit size " + itoa(bit_size)),
	}
}
